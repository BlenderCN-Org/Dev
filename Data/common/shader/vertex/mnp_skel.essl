//
// GLSL vertex shader with skinning.
// --------------------
//  m: mapping
//  n: normal
//  p: position
//

uniform mat4 boneMatrices[30];
varying vec2 mapping;
varying vec3 position;
varying vec3 normal;

void main(void)
{
   highp vec4  weights = gfxMultiTexCoord1;
   highp vec4  bones   = gfxMultiTexCoord2;

   highp vec4 p = vec4( 0.0, 0.0, 0.0, 0.0 );
   highp vec4 n = vec4( 0.0, 0.0, 0.0, 0.0 );

#if DYN_LOOP_SLOW
   p = p + (boneMatrices[int(bones.x)] * gfxVertex) * weights.x;
   n = n + (boneMatrices[int(bones.x)] * vec4(gfxNormal,0)) * weights.x;
   p = p + (boneMatrices[int(bones.y)] * gfxVertex) * weights.y;
   n = n + (boneMatrices[int(bones.y)] * vec4(gfxNormal,0)) * weights.y;
   p = p + (boneMatrices[int(bones.z)] * gfxVertex) * weights.z;
   n = n + (boneMatrices[int(bones.z)] * vec4(gfxNormal,0)) * weights.z;
   p = p + (boneMatrices[int(bones.w)] * gfxVertex) * weights.w;
   n = n + (boneMatrices[int(bones.w)] * vec4(gfxNormal,0)) * weights.w;
#else
   p = p + (boneMatrices[int(bones.x)] * gfxVertex) * weights.x;
   n = n + (boneMatrices[int(bones.x)] * vec4(gfxNormal,0)) * weights.x;
   if( weights.y > 0.0 )
   {
      p = p + (boneMatrices[int(bones.y)] * gfxVertex) * weights.y;
      n = n + (boneMatrices[int(bones.y)] * vec4(gfxNormal,0)) * weights.y;
      if( weights.z > 0.0 )
      {
         p = p + (boneMatrices[int(bones.z)] * gfxVertex) * weights.z;
         n = n + (boneMatrices[int(bones.z)] * vec4(gfxNormal,0)) * weights.z;
         if( weights.w > 0.0 )
         {
            p = p + (boneMatrices[int(bones.w)] * gfxVertex) * weights.w;
            n = n + (boneMatrices[int(bones.w)] * vec4(gfxNormal,0)) * weights.w;
         }
      }
   }
#endif

   mapping     = gfxMultiTexCoord0.xy;
   normal      = (gfxWorldMatrix * n).xyz;
   position    = (gfxWorldMatrix * p).xyz;
   gl_Position = gfxWorldViewProjectionMatrix * p;
}
